MODULE main 
VAR
	turn : boolean;
	proc0 : process user(turn, FALSE);
	proc1 : process user(turn, TRUE);
ASSIGN
	init(turn) := FALSE;
CTLSPEC EF (proc0.state = critical & proc1.state = critical)
CTLSPEC AG ! (proc0.state = critical & proc1.state = critical)
CTLSPEC AG (proc0.state = entering -> AF (proc0.state = critical))
CTLSPEC AG (proc0.state = exiting -> A [! (proc0.state = critical) U proc1.state = exiting])

MODULE user(turn, ind)
VAR
	state : {entering, critical, exiting, noncritical};
ASSIGN
	init(state) := {entering, noncritical};
	next(state) :=
		case
			(state = entering) & (turn = ind) : critical;
			state = critical : {critical, exiting};
			state = exiting : noncritical;
			state = noncritical : {entering, noncritical};
			TRUE : state;
		esac;
	next(turn) :=
		case 
			(state = exiting) & (ind = FALSE) : TRUE;
			(state = exiting) & (ind = TRUE) : FALSE;
			TRUE : turn;
		esac;
JUSTICE running
JUSTICE ! (state = critical)
JUSTICE ! (state = noncritical)